plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
}

def majorNumber = 1
def minorNumber = 1
def patchNumber = 0

def VERSION_NAME = "$majorNumber.$minorNumber.$patchNumber"
def VERSION_CODE = majorNumber * 100000 + minorNumber * 1000 + patchNumber * 10

android {
    compileSdkVersion   COMPILE_SDK_VERSION
    buildToolsVersion   BUILD_TOOL_VERSION

    defaultConfig {
        applicationId       "kr.pokeum.jsonviewer.app"
        minSdkVersion       MIN_SDK_VERSION
        targetSdkVersion    TARGET_SDK_VERSION
        versionCode         VERSION_CODE
        versionName         VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias        System.getenv("JSONVIEWER_KEYSTORE_ALIAS")
            keyPassword     System.getenv("JSONVIEWER_KEY_PASSWORD")
            storeFile       file(System.getenv("HOME") + "/keystore/jsonviewer_keystore.jks")
            storePassword   System.getenv("JSONVIEWER_STORE_PASSWORD")
        }
    }
    buildTypes {
        release {
            debuggable      false
            minifyEnabled   true
            signingConfig   signingConfigs.release
            proguardFiles   getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JAVA_COMPILE_VERSION
        targetCompatibility JAVA_COMPILE_VERSION
    }
    kotlinOptions {
        jvmTarget = JVM_TARGET
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation(project(":jsonviewer"))

    implementation stdlib.kotlin
    implementation androidx.core
    implementation androidx.appcompat
    implementation androidx.constraintLayout
    implementation material.material
    implementation coroutine.core
    implementation coroutine.android
    implementation network.okhttp

    implementation platform(firebase.bom)

    testImplementation test.junit

    androidTestImplementation androidTest.junit
    androidTestImplementation androidTest.espressoCore
}